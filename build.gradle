plugins {
    id 'java'
    id "com.gradleup.shadow" version "8.3.6"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

base {
    archivesName = archives_base_name
}

version = plugin_version
group = maven_group

processResources {
    filesMatching("plugin.yml") {
        expand "plugin_version": plugin_version,
                "bukkit_api_version": bukkit_api_version
    }
}

dependencies {

    implementation("com.googlecode.soundlibs:mp3spi:${mp3spi_version}") {
        exclude group: 'junit', module: 'junit'
    }
    shadow("com.googlecode.soundlibs:mp3spi:${mp3spi_version}") {
        exclude group: 'junit', module: 'junit'
    }

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation 'org.jflac:jflac-codec:1.5.2'
    shadow 'org.jflac:jflac-codec:1.5.2'

    // To use this dependency, you need to compile bukkit by yourself
    // See https://www.spigotmc.org/wiki/buildtools/
    // implementation "org.bukkit:craftbukkit:${bukkit_version}"

    // Use this dependency if you don't want to compile bukkit
    implementation "io.papermc.paper:paper-api:${bukkit_version}"
    implementation "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"
    shadow "dev.jorel:commandapi-bukkit-shade:${command_api_version}"
    // Next two are for testing of an upcoming CommandAPI change - Not yet available.
    //shadow "dev.jorel:commandapi-paper-shade:${command_api_version}"
    //shadow "dev.jorel:commandapi-paper-shade-mojang-mapped:${command_api_version}"

    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "5.3.0"
}

repositories {
    mavenCentral()
    maven { url = "https://repo.dmulloy2.net/repository/public/" }
    maven {
        name = "henkelmax.public"
        url = 'https://maven.maxhenkel.de/repository/public'
    }
    // You need this maven repository if you want to use the paper dependency
    maven {
        name = "papermc"
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
    maven { url = "https://repo.codemc.org/repository/maven-public/" }

    mavenLocal()
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set("dev")

    //relocate 'javazoom', "me.navoei.${mod_id}.javazoom"
    //relocate 'org.tritonus', "me.navoei.${mod_id}.tritonus"

    // By documentation, it was recommented to relocate to not cause issues with other plugins that shade CommandAPI
    relocate("dev.jorel.commandapi", "me.navoei.${mod_id}.commandapi")
}
